---
layout: page
title: "Martin-LÃ¶f Type Theory"
description: "A minimal Type Theory in Martin-LÃ¶f style"
chapter: 2
---

### Contents {#top}

1. [Universes](#universes)
1. [Finite Types](#finitetypes)
   1. [The Empty Type](#empty)
   1. [The One-Point Type](#onepoint)
   1. [Sum Types (Disjoint Union)](#sumtypes)
1. [Î£-Types](#sigma)
   1. [Cartesian Products](#cartesian)
   1. [Universal properties](#universal_cart_sigma)
1. [Î -Types](#pi)
   1. [Identity function and function composition](#functions)
   1. [Universal property for products and pullbacks](#pullback)
1. [Identity Types](#id)
   1. [Path Induction](#pathinduction)
   1. [Basic Operations with Identity Types](#basicop)
   1. [Reasoning with equality](#reasoning)
   1. [Lemmas](#lemmas)

--------------------------------------------------------

```agda
{-# OPTIONS --without-K --safe #-}

module mltt where
```

### Universes {#universes}

```agda
open import level renaming (zero to lzero; suc to lsuc) public
```
For the future, define one more level
```agda
1â„“ : Level
1â„“ = lsuc 0â„“
```

<p style="font-size: smaller; text-align: right">[top â‡‘](#top)</p>
---

### Finite Types {#finitetypes}

#### The Empty Type {#empty}

```agda
open import mltt.empty public
```
<p style="font-size: smaller; text-align: right">[top â‡‘](#top)</p>

#### The One-Point Type {#onepoint}

```agda
open import mltt.unit public
```
<p style="font-size: smaller; text-align: right">[top â‡‘](#top)</p>


#### Sum Types (Disjoint Union) {#sumtypes}

```agda
open import mltt.sum public
```

<p style="font-size: smaller; text-align: right">[top â‡‘](#top)</p>


### Î£-Types {#sigma}

Î£-Types generalize arbitrary disjoint unions. If we have a base `B : Set â„“` and a type family `(b : B) âŠ¢ E x : Set â„“'`, we form the type `Î£ B E` whose canonical elements are *pairs* $(b , e)$, where `b : B`, and `e : E b`. If the universe levels are different, `Î£ B E : Set (â„“ âŠ” â„“')`.

Instead of a `data` declaration of the form

    data Î£ {â„“ â„“'} (B : Set â„“) (E : B â†’ Set â„“') : Set (â„“ âŠ” â„“') where
      _,_ : (b : B) (e : E b) â†’ Î£ B E

we use a [`record`](https://agda.readthedocs.io/en/v2.6.1/language/record-types.html) type. Î£-Types can be translated into records, and viceversa, so we are not surrepticiously bringing unwanted features in the game. (An automatic translation is not available due to technical reasons.)

 The formation and introduction rules are:

```agda
--
record Î£ {â„“ â„“'} (B : Set â„“) (E : B â†’ Set â„“') : Set (â„“ âŠ” â„“') where
  constructor _,_ 
  field
    fst : B
    snd : E fst

infixr 50 _,_
```

Actually we do not need parentheses to write a pairâ€”a canonical term of the Î£-type: we could simply write `b , e`: recall, in the definition of the constructor, the *body* of the function is an infix comma. However, it is often convenient to put the parentheses in for visual clarity.

For better compatibility with the mathematical notation $âˆ‘_{x âˆˆ A} E_x$ (where the sum means coproduct, that is $âˆ_{x âˆˆ A} E_x$) it is convenient to use a [`syntax`](https://agda.readthedocs.io/en/v2.6.1/language/syntax-declarations.html) declaration:

```agda
syntax Î£ B (Î» x â†’ E) = Î£[ x âˆˆ B ] E
```

**Remark.** Defining the Î£-type as a record, we have a *named* access to the first and second components of a pair by way of the "field" part of the record declaration. Thus, if `u : Î£ B E`, then:

1. Since Î£ is a record, `Agda` will be able to (co)pattern match on the term `u` and
1. replace it with the pair `Î£.fst u , Î£.snd u`.

Thus the fields `fst` , `snd` act as projections. 

**Remark for Agda hackers.** Modules, and data type declarations, are *modules,* so `open public` them: this, for the Î£-type, would bring the names `fst` and `snd` in "scope": in this way, the pair corresponding to the term `u` would be `fst u , snd u`. We could have chosen names for the fields like `Ï€â‚` and `Ï€â‚‚`, that is, give a definition like

    record Î£ {â„“ â„“'} (B : Set â„“) (E : B â†’ Set â„“') : Set (â„“ âŠ” â„“') where
      constructor _,_ 
      field
        Ï€â‚ : B
        Ï€â‚‚ : E Ï€â‚

in which case the pair would simply be `Ï€â‚ , Ï€â‚‚`. This is very suggestive, but a bit confusing.

We will not usually open the module Î£ and we will define the projections on their own as follows.

```agda
-- projection to the base
Ï€â‚ : âˆ€ {â„“ â„“'} {B : Set â„“} {E : B â†’ Set â„“'} â†’ Î£ B E  â†’ B
Ï€â‚ (b , e) = b

-- "projection" to the fiber
Ï€â‚‚ : âˆ€ {â„“ â„“'} {B : Set â„“} {E : B â†’ Set â„“'} (t : Î£ B E) â†’ E (Ï€â‚ t)
Ï€â‚‚ (b , e) = e
```
As in many other cases, we can obtain the above expressions by just writing, say

    Ï€â‚ : âˆ€ {â„“ â„“'} {B : Set â„“} {E : B â†’ Set â„“'} â†’ Î£ B E  â†’ B
    Ï€â‚ u = {!!}

and using `C-c C-c` in Emacs to replace `u` with a pair. Then `C-c C-a` will fill in the hole automatically.

The **intuition** here is to imagine that if `(b : B) âŠ¢ E b` corresponds to a family $B âˆ‹ b \mapsto E_b$ of spaces, then the Î£-type `Î£ B E` corresponds to the *total space* $\coprod_{b âˆˆ B} E_b$ equipped with the projection
$$ \pi_1 \colon \coprod_{b âˆˆ B} E_b \to B$$
to the base


#### Induction and recursion for Î£-Types

The hardest is the type expression:

```agda
Î£-induction : âˆ€ {â„“ â„“' â„“''} {B : Set â„“} {E : B â†’ Set â„“'} {P : Î£ B E â†’ Set â„“''} â†’
              ( (b : B) â†’ (e : E b) â†’ P (b , e) ) â†’ 
              (u : Î£ B E) â†’ P u
```
whereas the proof is quite easy: write the hole

    Î£-induction g u = {!!}

and fill it in the usual way: `C-c C-c <var>` followed by `C-c C-a`
```agda
Î£-induction g (fst , snd) = g fst snd
```

Recursion is induction over a constant family, and we can, as usual, give both definitions
```agda
-- from induction
Î£-recursion : âˆ€ {â„“ â„“' â„“''} {B : Set â„“} {E : B â†’ Set â„“'} {P : Set â„“''} â†’
              ( (b : B) â†’ (e : E b) â†’ P ) â†’ Î£ B E â†’ P
Î£-recursion {P = P} = Î£-induction {P = (Î» _ â†’ P)}

-- direct definition
Î£-recursion' : âˆ€ {â„“ â„“' â„“''} {B : Set â„“} {E : B â†’ Set â„“'} {P : Set â„“''} â†’
              ( (b : B) â†’ (e : E b) â†’ P ) â†’ Î£ B E â†’ P
Î£-recursion' g (b , e) = g b e
```
As observed for sum types, of which Î£-types are a generalization, the recursion expresses the familiar universal property of coproducts. In sets (or other categories) if $\{ E_b \}_{b \in B}$ is a family of sets parametrized by $B$, and, say, $f_b \colon E_b \to P$ is a family of functions, then the universal property of coproducts says that there is a (unique) function
$f \colon \coprod_{b \in B} E_b \to P$, namely the one that to the pair $(b, e)$, where $e âˆˆ E_b$, assigns $f_b(e)$.

The `Î£-induction` is also referred to as <span style="color: teal">uncurry</span>, after [Haskell Curry](https://en.wikipedia.org/wiki/Haskell_Curry). There is an inverse:

```agda
curry : âˆ€ {â„“ â„“' â„“''} {B : Set â„“} {E : B â†’ Set â„“'} {P : Î£ B E â†’ Set â„“''} â†’
        ( (u : Î£ B E) â†’ P u ) â†’ 
        ( (b : B) â†’ (e : E b) â†’ P (b , e) )
curry f b e = f (b , e)
```
where a (dependent) function out of a Î£-type yields a two-variable function. (Note that the "uncurry" does exactly the opposite.)

#### Cartesian Products {#cartesian}

One very important special case is that of a Î£-type corresponding to a *non-dependent* function. That is just the **Cartesian Product**. This may seem unfamiliar at first glance, but if we have two, say, sets $A$ and $B$, then our familiar picture of $A Ã— B$ as the set of pairs $(a,b)$ corresponds exactly to the non-symmetric view of the same as the disjoint union  of copies of $B$ parametrized by elements of $A$. This is precisely the Î£-type corresponding to the constant family `(a : A) âŠ¢ B`. Thus we just state:

```agda
-- non-dependent Î£-type = Cartesian product
_Ã—_ : âˆ€{â„“ â„“'} (A : Set â„“) (B : Set â„“') â†’ Set (â„“ âŠ” â„“') 
A Ã— B = Î£ A (Î» x â†’ B)
```
Note that in Agda $A$ and $B$ are allowed to be in different universes.

The Cartesian product will inherit the induction and recursion principles from those for the more general Î£-types. For instance:
```agda
Ã—-induction : âˆ€ {â„“ â„“' â„“''} {A : Set â„“} {B : Set â„“'} {P : A Ã— B â†’ Set â„“''} â†’ 
               ((a : A) â†’ (b : B) â†’ P (a , b)) â†’ (x : A Ã— B) â†’ P x
Ã—-induction = Î£-induction 
```
but a direct definition is also possible
```agda
Ã—-induction' : âˆ€ {â„“ â„“' â„“''} {A : Set â„“} {B : Set â„“'} {P : A Ã— B â†’ Set â„“''} â†’ 
               ((a : A) â†’ (b : B) â†’ P (a , b)) â†’ (x : A Ã— B) â†’ P x
Ã—-induction' g (a , b) = g a b
```

Since neither type is dependent, we can implement the standard swap of the factors in the Cartesian product:
```agda
Ã—-swap : âˆ€ {â„“ â„“'} {A : Set â„“}{B : Set â„“'} â†’ A Ã— B â†’ B Ã— A
Ã—-swap (a , b) = b , a
```

```agda
Ã—-assoc : âˆ€ {â„“ â„“' â„“''} {A : Set â„“} {B : Set â„“'} {C : Set â„“''} â†’
    (A Ã— B) Ã— C â†’ A Ã— (B Ã— C)
Ã—-assoc (( a , b ) , c) = a , (b , c)
```
Once we have identity types, as an <span style="color: fuchsia">exercise</span> prove `Ã—-assoc` and `Ã—-swap` satisfy [Mac Lane's pentagonal and hexagonal diagrams](https://ncatlab.org/nlab/show/symmetric+monoidal+category) (plus certain additional higher diagrams).


#### Universal properties {#universal_cart_sigma}

We are used to think of the Cartesian product as a "product" in the categorical sense, in particular, the corresponding universal property involves a map *into* it, as opposed to one *out* of it, as it happens with the sum and the Î£-types. As we have seen, $A Ã— B$, *when interpreted as* $\coprod_A B$, indeed behaves as a coproduct and we have observed how its induction principle reflects the corresponding universal property.

However, since by *construction* the canonical terms of `A Ã— B` consist of pairs, we can prove that for any type `X` and `f : X â†’ A` and `g : X â†’ B` there is a (unique: this will be proved later) term of type `X â†’ A Ã— B`. Since we cannot yet prove the "uni" part, we call it "versal":
```agda
versal-cart : âˆ€ {Î½ â„“ â„“'} {X : Set Î½} {A : Set â„“} {B : Set â„“'}
              (f : X â†’ A) (g : X â†’ B) â†’ X â†’ (A Ã— B)
versal-cart f g x = f x , g x
```

There is nothing special about the Cartesian product, and the same works for Î£-types. In other words, we can write the very same kind of "versal" property for the dependent sum type:
```agda
versal-Î£ : âˆ€ {Î½ â„“ â„“'} {X : Set Î½} {B : Set â„“} {E : B â†’ Set â„“'} â†’
          (f : X â†’ B) â†’ ((x : X) â†’ E (f x)) â†’ X â†’ Î£ B E
versal-Î£ f g x = (f x) , (g x)
```
See [below](#pullback) for an interpretation. (Spoiler: this is a section of a pullback fibration.)

<p style="font-size: smaller; text-align: right">[top â‡‘](#top)</p>
---

### Î -Types {#pi}

Î -Types are built-in in Agda, with the notation âˆ€. We are going to define an alternative name, for symmetry of exposition

```agda
Î  : âˆ€ {â„“ â„“'} (B : Set â„“) (E : B â†’ Set â„“') â†’ Set (â„“ âŠ” â„“') 
Î  B E = (x : B) â†’ E x
```

This would probably not completely be in keeping with the form of MLTT we are sketching, where we need an explicit data declaration that expresses the *formation* and  *introduction* rules. The following does that (write Î â€² in Emacs as `\Pi\'`) 
```agda
data Î â€² {â„“ â„“' : Level} (B : Set â„“) (E : B â†’ Set â„“') : Set (â„“ âŠ” â„“') where
  dfun : ((x : B) â†’ E x) â†’ Î â€² B E
```
where we wrap the dependent function in a constructor called `dfun`. This is a bit impractical, in that we end up with essentially the same object wrapped up in an outer layer that will have to be stripped away in most applications.

Convenient syntax declarations:
```agda
syntax Î  B (Î» x â†’ E) = Î [ x âˆˆ B ] E
syntax Î â€² B (Î» x â†’ E) = Î â€²[ x âˆˆ B ] E
```
The standard function type `B â†’ C` is just a particular case of a `Î `-type when the family `(x : B) âŠ¢ E x` is constant with value `C`.

The elimination rules are of course going to seem a bit tautological:
```agda
Î -elim : âˆ€ {â„“ â„“'} {B : Set â„“} {E : B â†’ Set â„“'} {b : B} â†’ Î  B E â†’ E b
Î -elim {b = b} f = f b

Î â€²-elim : âˆ€ {â„“ â„“'} {B : Set â„“} {E : B â†’ Set â„“'} {b : B} â†’ Î â€² B E â†’ E b
Î â€²-elim {b = b} (dfun f) = f b

-- Same thing, but sometimes more convenient
Î â€²-elimâ‚ : âˆ€ {â„“ â„“'} {B : Set â„“} {E : B â†’ Set â„“'} â†’ Î â€² B E â†’ (b : B) â†’ E b 
Î â€²-elimâ‚ (dfun f) b = f b
```

In the analogy in which the Î£-type to `Î£ [b : B] E b` is viewed as the total space of a fibration, the Î -type `Î  [b : B] E b` ought to be the space of sections.

To define the latter:
```agda
Î“ : âˆ€ {â„“ â„“'} {B : Set â„“} {E : B â†’ Set â„“'} â†’ Î  B E â†’ (B â†’ Î£ B E)
Î“ f b = b , f b
```
Then we could prove that composing a section with the projection to the base is the identity operation

    gamma-is-sect: âˆ€ {â„“ â„“'} {B : Set â„“} {E : B â†’ Set â„“'}
                   (f : (b : B) â†’ E b) â†’ Ï€â‚ (Î“ f b) â‰¡ b

for which we will need to introduce `â‰¡` first. At that point it will be easy.

#### Identity and composition {#functions}

Since Î -types correspond to dependent functions, this is an appropriate place to introduce the identity function, and the function composition, copying them from the standard library. Let us use a *submodule* for this.

```agda
module function where
```
We are declaring some variables to avoid repetitions
```agda
  private
    variable
      â„“ â„“' â„“'' : Level
      A : Set â„“
      B : Set â„“'
      C : Set â„“''
```
Then the identity function is the obvious one:
```agda
  -- identity function
  id : A â†’ A
  id x = x
```
whereas for function composition we can define a dependent and a non-dependent version. We start with the first, and simply define the latter in terms of the former
```agda
  -- dependent composition
  infixr 9 _âˆ˜_
  _âˆ˜_ : âˆ€ {A : Set â„“} {B : A â†’ Set â„“'} {C : {x : A} â†’ B x â†’ Set â„“''} â†’
        (âˆ€ {x} (y : B x) â†’ C y) â†’ (g : (x : A) â†’ B x) â†’
        ((x : A) â†’ C (g x))
  f âˆ˜ g = Î» x â†’ f (g x)

  -- non-dependent version (primed â€²=\')
  infixr 9 _âˆ˜â€²_
  _âˆ˜â€²_ : (B â†’ C) â†’ (A â†’ B) â†’ (A â†’ C)
  f âˆ˜â€² g = f âˆ˜ g
```
Then we `open` the module to be able to use the naked names, in place of writing things like `function.id`.
```agda
open function public
```

This is also a good place to introduce certain convenience functions. Once again, let us encapsulate them in a module:

```agda
module convenience where
```
Trick to recover implicit arguments from functions without the need of giving them as arguments
```agda
  domain : âˆ€{â„“ â„“'} {A : Set â„“} {B : Set â„“'} â†’ (A â†’ B) â†’ Set â„“
  domain {â„“} {â„“'} {A} {B} f = A

  codomain : âˆ€{â„“ â„“'} {A : Set â„“} {B : Set â„“'} â†’ (A â†’ B) â†’ Set â„“'
  codomain {â„“} {â„“'} {A} {B} f = B
```
Evaluation maps are a standard thing
```agda
  ev' : âˆ€ {â„“ â„“'} {A : Set â„“} {E : A â†’ Set â„“'} (x : A) â†’ ((x : A) â†’ E x) â†’ E x
  ev' x f = f x

  ev : âˆ€ {â„“ â„“'} {A : Set â„“} {E : A â†’ Set â„“'} â†’ ((x : A) â†’ E x) â†’ (x : A) â†’ E x
  ev f x = f x
```
This allows to recover the type of a term
```agda
  type-of : âˆ€{â„“} {A : Set â„“} â†’ A â†’ Set â„“
  type-of {â„“} {A} x = A
```
then, open the module to expose the names in it:
```agda
open convenience public
```

#### Universal property for products and pullbacks {#pullback}

The signature of `versal-Î£` above can be written in terms of function composition:
```agda
versal-Î£' : âˆ€ {Î½ â„“ â„“'} {X : Set Î½} {B : Set â„“} {E : B â†’ Set â„“'} â†’
          (f : X â†’ B) â†’ ((x : X) â†’ (E âˆ˜ f) x) â†’ X â†’ Î£ B E
versal-Î£' f g x = (f x) , (g x)
```
Typing `C-c C-n` (=normalize expression) in Emacs to bring up a dialog with Agda we can see that both `versal-Î£` and `versal-Î£'` normalize (i.e. they are computed to) the same canonical expression.

The type family `(x : X) âŠ¢ (E âˆ˜ f) x` ought to be considered as giving rise to a fibration over `X`, namely the pullback of `(b : B) âŠ¢ E b` to `X`. Then the type `X â†’ Î£ B E` must be identified with that of sections of the pull back fibration, whose total space will be the Î£-type `Î£ X (E âˆ˜ f)`. First, there is an evident map of total spaces
```agda
map-over-total : âˆ€ {Î½ â„“ â„“'} {X : Set Î½} {B : Set â„“} {E : B â†’ Set â„“'} â†’
       (f : X â†’ B) â†’ Î£ X (E âˆ˜ f) â†’ Î£ B E
map-over-total f (x , v) = (f x) , v
```
Second, we can compute the composition of this with the above defined `Î“`. We feed `Î“` the term `s : Î  X (E âˆ˜ f)` i.e `s : (x : X) âŠ¢ E (f x)` and apply `map-over-total` to it: 
```agda
map-section : âˆ€ {Î½ â„“ â„“'} {X : Set Î½} {B : Set â„“} {E : B â†’ Set â„“'} â†’
      (f : X â†’ B) â†’ Î  X (E âˆ˜ f) â†’ X â†’ Î£ B E
map-section {Î½} {â„“} {â„“'} {X} {B} {E} f s = (map-over-total f) âˆ˜ (Î“ s)
```
If you calculate the normalization of this expression you will see that it is definitionally equal to `versal-Î£`.

<p style="font-size: smaller; text-align: right">[top â‡‘](#top)</p>
---

### Identity Types {#id}

In Agda core, or in its Standard Library the identity type is defined as follows:
```agda
infix 4 _â‰¡_  
data _â‰¡_ {â„“} {A : Set â„“} (x : A) : A â†’ Set â„“ where
  instance refl : x â‰¡ x

{-# BUILTIN EQUALITY _â‰¡_ #-}
```
It is common to use alternative names for the identity paths, such as
```agda
Id : {â„“ : Level} (A : Set â„“) â†’ A â†’ A â†’ Set â„“
Id {â„“} A = _â‰¡_ {â„“} {A}
```
as well as an explicit version of the reflexivity:
```agda
-- explicit version
idp : {â„“ : Level} {A : Set â„“} (x : A) â†’ x â‰¡ x
idp x = refl {x = x}
```
It is also common to see the name `paths` used in place of `â‰¡`, owing to the topological interpretation. Notice as well that the notational convention about `=` (definitional equality) and `â‰¡` (propositional equality) is opposite to that used in the HoTT book. 

It is convenient to be able to recover implicit arguments in equalities (identities) without mentioning them.
```agda
lhs : âˆ€{â„“} {A : Set â„“} {x y : A} â†’ x â‰¡ y â†’ A
lhs {â„“} {A} {x} {y} p = x

rhs : âˆ€{â„“} {A : Set â„“} {x y : A} â†’ x â‰¡ y â†’ A
rhs {â„“} {A} {x} {y} p = y
```
The idea is to be able to write `lhs p` to refer to `x` above.

Note that the identity type is a genuine *family* indexed by the terms of a given type.

#### Path induction {#pathinduction}

The induction principle for the identity types, often called path induction, is given by Martin-LÃ¶f in the following form
```agda
â‰¡-induction : âˆ€ {â„“ â„“'} {A : Set â„“} (D : (x y : A) â†’ x â‰¡ y â†’ Set â„“') â†’
              ((x : A) â†’ D x x refl) â†’ ( (x y : A) â†’ (p : x â‰¡ y) â†’ D x y p )
```
The proof is by computation of `p : x â‰¡ y` to the canonical term `refl : x â‰¡ x`. Interactively, we have

    â‰¡-induction D d x y p = {!!}

Then, we can do a case splitting (with `C-c C-c` in Emacs) on `p` which yields

    â‰¡-induction D d x .x refl = {!!}

which is filled automatically by Agda:
```agda
â‰¡-induction D d x .x refl = d x
```
The oddly looking `.x`, what Agda calls an "irrefutable pattern," also "dot-pattern," is due to the following: if `p=refl` (definitionally) then `y=x` (again definitionally). Agda signals this fact by pre-pending a dot; `.x` means that the expression is there to only satisfy the type-checking process.

In keeping with Martin-LÃ¶f own notation we call this `ğ•`
```agda
ğ• = â‰¡-induction
```
<!--
There is a commonly used "pointed" variant of `ğ•`, called "based path induction" in the HoTT book, where we isolate one of the two terms in the type:
```agda-ignore
â„ : âˆ€ {â„“ â„“'} {A : Set â„“} (x : A) (C : (y : A) â†’ x â‰¡ y â†’ Set â„“') â†’ 
    C x refl â†’ (y : A) â†’ (p : x â‰¡ y) â†’ C y p
â„ x C c .x refl = c
```
These two versions agree. First, we can obtain `ğ•` from `â„` in the following way
```agda-ignore
-- Standard path induction from the based one
ğ•' : âˆ€ {â„“ â„“'} {A : Set â„“} (D : (x y : A) â†’ x â‰¡ y â†’ Set â„“') â†’ 
     ((x : A) â†’ D x x refl) â†’ (x y : A) â†’ (p : x â‰¡ y) â†’ D x y p
ğ•' D d x y p = â„ x (D x) (d x) y p
-- ğ•' D d x = â„ x (D x) (d x)
```
Then check that `ğ•` and `ğ•'` agree:
```agda-ignore
ğ•-to-ğ•' : âˆ€ {â„“ â„“'} {A : Set â„“} (D : (x y : A) â†’ x â‰¡ y â†’ Set â„“')  
          (d : (x : A) â†’ D x x refl) (x y : A) (p : x â‰¡ y) â†’
          ğ• D d x y p â‰¡ ğ•' D d x y p
```
if we put a hole and simply case-split (`C-c C-c`) on `p` we get

    ğ•-to-ğ•' D d x .x refl = {!!}

If we normalize (`C-u C-u C-c C-,`) the goal, Agda shows that we must prove `d x â‰¡ d x`, a case for filling the hole with `refl`:
```agda-ignore
ğ•-to-ğ•' D d x .x refl = idp (d x) -- signal which reflexivity with an explicit term
```

Conversely, we can define the based path induction from the standard one without using pattern matching on `p`. This is considerably *harder*
```agda-ignore
â„' : âˆ€ {â„“ â„“'} {A : Set â„“} (x : A) (C : (y : A) â†’ x â‰¡ y â†’ Set â„“') â†’ C x refl â†’
     (y : A) â†’ (p : x â‰¡ y) â†’ C y p
â„' {â„“} {â„“'} {A} x C c y p = ğ• ğ”» (Î» x C c â†’ c) x y p C c where 
  ğ”» : âˆ€ {â„“ â„“'} {A : Set â„“} â†’ (x y : A) â†’ (p : x â‰¡ y) â†’ Set (â„“ âŠ” lsuc â„“')
  ğ”» {â„“} {â„“'} {A} x y p = (C : ((y' : A) â†’ (p' : x â‰¡ y') â†’ Set â„“')) â†’ (C x refl â†’ C y p)
```
-->

#### Basic Operations with Identity Types {#basicop}

We will refer to terms of an identity type as "identifications." Thus, if `p : x â‰¡ y`, then we will refer to `p` as an identification of `x` with `y`. Of course, owing to the upcoming homotopical interpretation, we will also refer to `p` as a "path" from `x` to `y`.

Once we have the induction principle for the identity types, we can immediately define certain basic operations with them:

  1. [*transport*](#transport) along an identification,
  1. [*application*](#ap) of a function to an identification, and
  1. [*composition* and *inversion*](#composition) of identifications.

More operations that flesh out the homotopical interpretation will be defined later. There are also lemmas that govern these operations and how they interact. These lemmas, proved below, give meaning to the slogan "Types are higher groupoids."

##### Transport {#transport}

If `P : A â†’ Set â„“` is a dependent type, the `transport` along `p : x â‰¡ y` results in a map `P x â†’ P y`. This is the analog of the (parallel) transport along a path in the base of a fibration. We are going to provide two proofs of if. First, the harder inductive one using the `ğ•`-induction principle.
```agda
transportğ• : âˆ€{â„“ â„“'} {A : Set â„“} (P : A â†’ Set â„“') {x y : A}
            (p : x â‰¡ y) â†’ P x â†’ P y
transportğ• {â„“} {â„“'} {A} P {x} {y} p = ğ• D d x y p 
  where
    D : âˆ€{â„“ â„“'} {A : Set â„“} {P : A â†’ Set â„“'} (x y : A) â†’ x â‰¡ y â†’ Set â„“'
    D {P = P} x y p = P x â†’ P y
    d : âˆ€{â„“ â„“'} {A : Set â„“} {P : A â†’ Set â„“'} (x : A) â†’ D {P = P} x x refl
    d x = id
```
In the above, we used the `where` keyword: [check it out](https://agda.readthedocs.io/en/v2.6.1/language/let-and-where.html?highlight=where#where-blocks). This gets easier if we pattern-match on `p`:
```agda
transport : âˆ€{â„“ â„“'} {A : Set â„“} (P : A â†’ Set â„“') {x y : A}
            (p : x â‰¡ y) â†’ P x â†’ P y
transport P refl = id
```
Of course the two versions agree, in the following sense
```agda
transport-agreement : âˆ€ {â„“ â„“'} {A : Set â„“} (P : A â†’ Set â„“') {x y : A} (p : x â‰¡ y) â†’
                      transport P p â‰¡ transportğ• P p
transport-agreement P refl = idp (id)
```

##### Applicative {#ap}

If we have an identification `p : x â‰¡ y`, where `x y : A`, and `f : A â†’ B`, then we can inquire about the "image" of the path in `B` under the mapping `f`, in other words, that we get a path `ap f p : f x â‰¡ f y` between the images. Again, this can be done in two ways; by pattern matching:

```agda
ap : âˆ€ {â„“ â„“'} {A : Set â„“} {B : Set â„“'} {x y : A} (f : A â†’ B) â†’ 
     x â‰¡ y â†’ f x â‰¡ f y
ap f refl = refl
```
and with path induction:
```agda
apğ• : âˆ€ {â„“ â„“'} {A : Set â„“} {B : Set â„“'} {x y : A} (f : A â†’ B) â†’
      x â‰¡ y â†’ f x â‰¡ f y
apğ• {â„“} {â„“'} {A} {B} {x} {y} f p = ğ• D d x y p where
  D : âˆ€ {â„“ â„“'} {A : Set â„“} {B : Set â„“'} {f : A â†’ B} (x y : A) (p : x â‰¡ y) â†’ Set â„“'
  D {f = f} x y p = f x â‰¡ f y
  d : âˆ€ {â„“ â„“'} {A : Set â„“} {B : Set â„“'} {f : A â†’ B} (x : A) â†’ D {f = f} x x refl
  d x = refl
```
There is a dependent variant of `ap`, `apd`, which we will define later. For now, we check that the two versions we have just defined agree:
```agda
ap-agreement : âˆ€ {â„“ â„“'} {A : Set â„“} {B : Set â„“'} {x y : A} (f : A â†’ B) (p : x â‰¡ y) â†’ ap f p â‰¡ apğ• f p
ap-agreement f refl = idp refl
```

##### Composition and inversion of identifications {#composition}

This is one of the most interesting parts. Composition and inversion of identifications tie the formalism of equality as an equivalence relation with the intuition that paths in a space form a (higher) groupoid.

We start from the *inversion*. First, the induction version:
```agda
â‰¡-invğ• : âˆ€ {â„“} {A : Set â„“} {x y : A} â†’ x â‰¡ y â†’ y â‰¡ x
â‰¡-invğ• {â„“} {A} {x} {y} p = ğ• (Î» x y p â†’ y â‰¡ x) (Î» x â†’ refl) x y p
```
And of course we have another proof based on pattern matching on the constructor
```agda
infixr 30 _â»Â¹
_â»Â¹ : âˆ€ {â„“} {A : Set â„“} {x y : A} â†’ x â‰¡ y â†’ y â‰¡ x
refl â»Â¹ = refl
```
The postfix notation was chosen to reflect that fact that this is the construction of the *inverse path* in homotopy theory. For symmetry:
```agda
â‰¡-inv = _â»Â¹
```
As for *composition*, this is the composition of two successive identifications, the intuition behind it being that this operation should correspond to path concatenation in topology. As usual, following the HoTT book, we begin with a proof of the composition operation via path induction:
```agda
â‰¡-compğ• : âˆ€ {â„“} {A : Set â„“} {x y z : A} (p : x â‰¡ y ) â†’ y â‰¡ z â†’ x â‰¡ z
â‰¡-compğ• {â„“} {A} {x} {y} {z} p = ğ• D d x y p z where 
    D : âˆ€ {â„“} {A : Set â„“} (x y : A) (p : x â‰¡ y) â†’ Set â„“
    D {â„“} {A} x y p = (z : A) â†’ (q : y â‰¡ z) â†’ x â‰¡ z

    d : âˆ€ {â„“} {A : Set â„“} (x : A) â†’ D x x refl -- Î [ z âˆˆ A ] Î [ q âˆˆ x â‰¡ z ] (x â‰¡ z)
    d {â„“} {A} x z q = ğ• E (Î» x â†’ refl) x z q where
        E : âˆ€ {â„“} {A : Set â„“} â†’ Î [ x âˆˆ A ] Î [ z âˆˆ A ] Î [ q âˆˆ x â‰¡ z ] Set â„“
        E {â„“} {A} x z q = x â‰¡ z
```
A second proof (this is also in the HoTT book) uses the induction or pattern matching on one of the constructors.
```agda
infix 25 _â—¾_
_â—¾_ : âˆ€ {â„“} {A : Set â„“} {x y z : A} â†’
      x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
refl â—¾ q = q
```
<span style="color: teal; font-weight: bold">Remark</span> Note how, from the induction principle,  we have **proved** both the *symmetric* and *transitive* properties of propositional equality.

<span style="color: teal; font-weight: bold">Remark</span> Note that the path composition is in *lexicographical order,* not in the standard function-theoretic one.

Note how the proof of `_â—¾_` is asymmetric. We can always define the same operation with a different proof
```agda
_â—¾â€²_ : âˆ€ {â„“} {A : Set â„“} {x y z : A} â†’
      x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
p â—¾â€² refl = p
```
Of course we should give a proof they agree:
```
â—¾-agreement : âˆ€ {â„“} {A : Set â„“} {x y z : A} (p : x â‰¡ y) (q : y â‰¡ z) â†’ p â—¾ q â‰¡ p â—¾â€² q
â—¾-agreement refl refl = refl
```


#### Reasoning with equality {#reasoning}

One drawback of writing proofs in Agda is that, say, pattern-matching destroys the "history" of the proof and only leaves the final result, whereas it would be beneficial to be able to record the *steps* we make in a chain of deductionsâ€”pretty much in the same way as we would while writing proofs on paper.

There is a lovely technique, used in many spots in the Agda Standard Library, for example, which provides for recording the *reason* as to why some inference holds. It allows to use a chain of deductions based on  propositional equality `â‰¡`.

We reimplement it from scratch here:

```agda
module â‰¡-Reasoning {â„“ : Level} {A : Set â„“}  where

  infix 3 _âˆ
  infixr 2 _â‰¡âŸ¨_âŸ©_
  
  _â‰¡âŸ¨_âŸ©_ : (x : A) {y z : A} â†’
           x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
  x â‰¡âŸ¨ p âŸ© q = p â—¾ q

  _âˆ : (x : A) â†’ x â‰¡ x
  x âˆ = idp x
```
`_â—¾` is a postfix function assigning to a term `x : A` its reflexivity term in `x â‰¡ x`. The other, `_â‰¡âŸ¨_âŸ©_`, is a modified version of transitivity.

What these two functions do is to allow this form of writing:

    x â‰¡âŸ¨ p âŸ© 
    y â‰¡âŸ¨ q âŸ©
    z âˆ 

We are going to see instances of `â‰¡-Reasoning` at work in lemmas about `transport`, `ap`, and `_â—¾_`.


#### Some lemmas {#lemmas}

First, let us establish the usual identities, that is, those familiar from Topology, for path operations.

```agda
-- Lemmas about _â—¾_ (path composition)

module â—¾-lemmas where

  private
    variable
      â„“ : Level
      A : Set â„“
      x y : A

  -- refl is a left-unit
  lu : (p : x â‰¡ y) â†’ idp (lhs p) â—¾ p â‰¡ p
  lu p = idp p

  -- refl is a right-unit
  ru : (p : x â‰¡ y) â†’ p â—¾ idp (rhs p) â‰¡ p
  ru refl = refl

  lu=ru : {x : A} â†’ lu (idp x) â‰¡ ru (idp x)
  lu=ru = idp (refl)

  -- right inverse
  rinv : (p : x â‰¡ y) â†’ p â—¾ p â»Â¹ â‰¡ idp (lhs p)
  rinv refl = refl

  -- left inverse
  linv : (p : x â‰¡ y) â†’ p â»Â¹ â—¾ p â‰¡ idp (rhs p)
  linv refl = refl

  -- taking the inverse twice is the identity
  invÂ²-id : (p : x â‰¡ y) â†’ (p â»Â¹) â»Â¹ â‰¡ p
  invÂ²-id {x = x} refl = idp (refl {x = x})

  -- composition is associative
  assoc : {z w : A} (p : x â‰¡ y) (q : y â‰¡ z) (r : z â‰¡ w) â†’ (p â—¾ q) â—¾ r â‰¡ p â—¾ (q â—¾ r)
  assoc refl q r = idp (q â—¾ r)
```
One should be aware that the proof of associativity produces a term of a certain identity type, namely `(p â—¾ q) â—¾ r â‰¡ p â—¾ (q â—¾ r)`. Both the left and right hand side are identifications of `x` with `z` as terms of `A`. In turn, the witness of the associativity will satisfy higher identities (pentagons and up).

<span style="color: fuchsia">Exercise.</span> Define functions with the following type signatures

      assocğ• : assocğ• : {x y z w : A} (p : x â‰¡ y) â†’ (q : y â‰¡ z) â†’ (r : z â‰¡ w) â†’ (p â—¾ q) â—¾ r â‰¡ p â—¾ (q â—¾ r)
      assocğ• = {!!}

      invÂ²-idğ• : {x y : A} (p : x â‰¡ y) â†’ (p â»Â¹) â»Â¹ â‰¡ p
      invÂ²-idğ• = {!!}

where you prove the same statements using Martin-LÃ¶f's ğ•-induction, instead of pattern matching on the variables. (The proofs in the ğ•-style can be found in the HoTT book.)

The operation `ap` maps a path via a function. Thus we expect it to have certain functorial properties relative to the path operations, which we prove in the following module:
```agda
module ap-lemmas where

  private
    variable
      â„“ â„“' â„“'' : Level
      A : Set â„“
      B : Set â„“'
      C : Set â„“''
      x y z : A

  open â—¾-lemmas

  apâ—¾ : (f : A â†’ B) (p : x â‰¡ y) (q : y â‰¡ z) â†’ ap f (p â—¾ q) â‰¡ (ap f p) â—¾ (ap f q)
  apâ—¾ f refl q = idp (ap f q)

  -- destruct q
  -- use reasoning
  apâ—¾' : (f : A â†’ B) (p : x â‰¡ y) (q : y â‰¡ z) â†’ ap f (p â—¾ q) â‰¡ (ap f p) â—¾ (ap f q)
  apâ—¾' f p refl = ap f (p â—¾ refl) â‰¡âŸ¨ ap (ap f) (ru p) âŸ©
                  ap f p â‰¡âŸ¨ ru (ap f p) â»Â¹ âŸ©
                  (ap f p) â—¾ refl âˆ
    where
      open â‰¡-Reasoning

  apâ—¾â€² : (f : A â†’ B) (p : x â‰¡ y) (q : y â‰¡ z) â†’ ap f (p â—¾â€² q) â‰¡ (ap f p) â—¾â€² (ap f q)
  apâ—¾â€² f p refl = idp (ap f p)

  apâ»Â¹ : (f : A â†’ B) (p : x â‰¡ y) â†’ ap f (p â»Â¹) â‰¡ (ap f p) â»Â¹
  apâ»Â¹ f refl = refl

  apâˆ˜ : (f : A â†’ B) (g : B â†’ C) (p : x â‰¡ y) â†’
        ap (g âˆ˜â€² f ) p â‰¡ ap g (ap f p)
  apâˆ˜ f g refl = refl

  apid : (p : x â‰¡ y) â†’ ap id p â‰¡ p
  apid refl = refl

  apconst : (p : x â‰¡ y) (b : B) â†’ ap (Î» (x : A) â†’ b) p â‰¡ refl
  apconst refl b = refl

```

We have a similar situation with the transport operation. If `P : A â†’ Set â„“'`, the operation `transport` assigns to an identification `p : x â‰¡ y` in the base type a function `P x â†’ P y`. In the following modules we study some of its properties relative to path and function composition.
```agda
module transport-lemmas where

  private
    variable
      â„“ â„“' : Level
      A : Set â„“
      P : A â†’ Set â„“'

  -- transport is compatible with path composition
  transportâ—¾ : {x y z : A} (p : x â‰¡ y) (q : y â‰¡ z) {u : P x} â†’ 
               ( (transport P q) âˆ˜â€² (transport P p) ) u â‰¡ transport P (p â—¾ q) u
  transportâ—¾ refl q = refl

  -- transport is compatible with function composition
  transportâˆ˜ : âˆ€{Î½} {X : Set Î½} {x y : X} {f : X â†’ A} 
               (p : x â‰¡ y) {u : P (f x)} â†’ transport (P âˆ˜â€² f) p u â‰¡ transport P (ap f p) u
  transportâˆ˜ refl = refl

  -- transport is compatible with morphisms of fibrations (f : Î [ x âˆˆ A ] (P x â†’ Q x))
  transport-cov : âˆ€ {â„“''} {Q : A â†’ Set â„“''} {x y : A} {f : (x : A) â†’ P x â†’ Q x}
                   (p : x â‰¡ y) {u : P x} â†’ transport Q p (f x u) â‰¡ f y (transport P p u)
  transport-cov {x = x} {f = f} refl {u} = idp (f x u)

  -- state transport-cov without the u : P x
  transport-cov' : âˆ€ {â„“''} {Q : A â†’ Set â„“''} {x y : A} {f : (x : A) â†’ P x â†’ Q x}
                   (p : x â‰¡ y) â†’ transport Q p âˆ˜ (f x) â‰¡ (f y) âˆ˜ (transport P p)
  transport-cov' refl = refl

  -- compatibility with identifications of path
  transportâ‰¡ : {x y : A} {p q : x â‰¡ y} (Î± : p â‰¡ q) {u : P x} â†’
              transport P p u â‰¡ transport P q u
  transportâ‰¡ refl = refl
```

A useful special case is that of transport in a family of identity types. Namely `P : A â†’ Set â„“'` where `P x` is a type of paths, perhaps in `A` itself, which is the simplest case to consider.

```agda
module transport-in-paths where

  private
    variable
      â„“ : Level
      A : Set â„“

  open â—¾-lemmas

  transport-path-f : {a x y : A} (p : x â‰¡ y) (q : a â‰¡ x) â†’  
                     transport (Î» v â†’ a â‰¡ v) p q â‰¡ q â—¾ p
  transport-path-f refl q = ru q â»Â¹

  transport-path-b : {a x y : A} (p : x â‰¡ y) (q : x â‰¡ a) â†’
                     transport (Î» v â†’ v â‰¡ a) p q â‰¡ p â»Â¹ â—¾ q
  transport-path-b refl q = refl

  transport-path-id : {x y : A} (p : x â‰¡ y) (q : x â‰¡ x) â†’
                      transport (Î» v â†’ v â‰¡ v) p q â‰¡ (p â»Â¹ â—¾ q) â—¾ p
  transport-path-id refl q = ru q â»Â¹

  transport-path-id' : {x y : A} (p : x â‰¡ y) (q : x â‰¡ x) â†’
                       transport (Î» v â†’ v â‰¡ v) p q â‰¡ p â»Â¹ â—¾ (q â—¾ p)
  transport-path-id' refl q = ru q â»Â¹

  transport-path-parallel : âˆ€ {â„“'} {B : Set â„“'} {f g : A â†’ B} {x y : A}
                            (p : x â‰¡ y) (q : f x â‰¡ g x) â†’
                            transport (Î» v â†’ f v â‰¡ g v) p q â‰¡ ((ap f p) â»Â¹ â—¾ q) â—¾ (ap g p)
  transport-path-parallel refl q = ru q â»Â¹

  transport-path-parallel' : âˆ€ {â„“'} {B : Set â„“'} {f g : A â†’ B} {x y : A}
                             (p : x â‰¡ y) (q : f x â‰¡ g x) â†’
                             transport (Î» v â†’ f v â‰¡ g v) p q â‰¡ (ap f p) â»Â¹ â—¾ (q â—¾ ap g p)
  transport-path-parallel' refl q = ru q â»Â¹

  transport-path-fun-left : âˆ€ {â„“'} {B : Set â„“'} {f : A â†’ B} {b : B} {x y : A}
                            (p : x â‰¡ y) (q : f x â‰¡ b) â†’
                            transport (Î» v â†’ f v â‰¡ b) p q â‰¡ (ap f p) â»Â¹ â—¾ q
  transport-path-fun-left refl q = refl

  transport-path-fun-right : âˆ€ {â„“'} {B : Set â„“'} {b : B} {g : A â†’ B} {x y : A}
                            (p : x â‰¡ y) (q : b â‰¡ g x) â†’
                            transport (Î» v â†’ b â‰¡ g v) p q â‰¡  q â—¾ ap g p
  transport-path-fun-right refl q = ru q â»Â¹

```

<p style="font-size: smaller; text-align: right">[top â‡‘](#top)</p>
---
